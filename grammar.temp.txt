Start -> Module
Module -> ModuleDefine ModuleStmts
ModuleDefine ->  ModuleSelfDefine | ModuleSelfDefine ModuleImportDefineList | ModuleSelfDefine ModuleExportList | ModuleSelfDefine ModuleImportDefineList ModuleExportList
ModuleSelfDefine -> AT MODULE COLON ID SEMI
ModuleImportDefineList -> ModuleImportDefineList ModuleImportDefine | ModuleImportDefine
ModuleImportDefine -> IMPORT COLON ID SEMI
ModuleExportList -> ModuleExportList ModuleExport | ModuleExport
ModuleExport ->  EXPORT COLON ID SEMI
ModuleStmts -> ModuleStmts VariableDef | ModuleStmts FunDefStmt  | FunDefStmt | VariableDef | E

FunDefStmt -> FunDef | STATIC FunDef
FunDef -> FUN ID LEFT_PAREN ParamList RIGHT_PAREN BlockStmt
ParamList -> ID | ID COMMA ParamList | E

Stmts -> Stmts Stmt | E
Stmt -> BlockStmt | VariableDef | InnerFunDefStmt | IfStmt | WhileStmt | ReturnStmt | AssignStmt | Exp SEMI
BlockStmt -> LEFT_BRACE Stmts RIGHT_BRACE

VariableDef -> STATIC VarDefStmt | VarDefStmt
VarDefStmt -> LET ID ASSIGN Exp SEMI | LET ID SEMI

VariableExp -> ID | AT ID

InnerFunDefStmt -> AT FunDef
IfStmt -> IF LEFT_PAREN Exp RIGHT_PAREN Stmt | IfStmt ELSE Stmt
WhileStmt -> WHILE LEFT_PAREN Exp RIGHT_PAREN Stmt
ReturnStmt -> RETURN Exp SEMI | RETURN SEMI
AssignStmt -> VariableExp ASSIGN Exp SEMI | ArrayMemberExp ASSIGN Exp SEMI


Exp ->  ArrayExp | ArrayMemberExp | CalExp

ArrayExp -> LEFT_BRACKET ArrayItems RIGHT_BRACKET
ArrayItems -> ArrayItem | ArrayItem COMMA ArrayItems | E
ArrayItem -> Literal | VariableExp | ArrayExp | CallExp

ArrayMemberExp ->  VariableExp ArraySub
ArraySub -> LEFT_BRACKET Exp RIGHT_BRACKET | LEFT_BRACKET Exp RIGHT_BRACKET ArraySub

CallExp -> ID LEFT_PAREN ArgumentList RIGHT_PAREN | AT ID LEFT_PAREN ArgumentList RIGHT_PAREN | ID MODULE_SCOPE CallExp | ID DOT CallExp
ArgumentList -> Exp | ArgumentList COMMA Exp | E

CalExp -> CalExp LogicOperator LogicExp | LogicExp
LogicOperator -> LOGIC_OR | LOGIC_AND

LogicExp -> LogicExp BitOperator BitExp | BitExp
BitOperator -> BIT_AND | BIT_OR

BitExp -> BitExp RelationalOperator  RelationExp | RelationExp
RelationalOperator -> LESS | LESS_EQUAL | EQUAL | NOT_EQUAL | GREATER | GREATER_EQUAL

RelationExp -> RelationExp AdditiveOperator AdditiveExp | AdditiveExp
AdditiveOperator -> ADD | SUB

AdditiveExp -> AdditiveExp FactorOperator FactorExp | FactorExp
FactorOperator -> MOD | DIV | MUL

FactorExp -> UnaryBeforeOperator UnaryExp | UnaryExp UnaryAfterOperator | UnaryExp
UnaryBeforeOperator -> BIT_NOT | NOT
UnaryAfterOperator -> ADD_ONE | SUB_ONE

UnaryExp -> Literal | VariableExp | LEFT_PAREN Exp RIGHT_PAREN | CallExp
Literal ->  NUMBER | STRING | TRUE | FALSE | NULL